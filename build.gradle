buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

group = 'com.jme3'
version = 'jre8-2.0.0'

compileJava {
    options.encoding = "UTF-8"
    options.incremental = true
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext.rlibVersion = "7.0.0"
ext.bintrayVersion = version

def jme3 = [v: '3.2.0-stable', g: 'org.jmonkeyengine']

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
    maven {
        url "https://jitpack.io"
    }
    maven {
        url  "https://dl.bintray.com/javasabr/maven"
    }
}

publishing {
    publications {
        LibArtifact(MavenPublication) {
            from components.java
            groupId group
            artifactId 'jfx'
            version bintrayVersion
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = System.getProperty('bintrayUser')
    key = System.getProperty('bintrayKey')
    publications = ['LibArtifact']
    pkg {
        repo = 'maven'
        name = 'com.jme3.jfx'
        licenses = ['Apache-2.0']
        websiteUrl = 'https://github.com/TalosDx/J8-JME-JFX'
        vcsUrl = 'https://github.com/TalosDx/J8-JME-JFX.git'
        issueTrackerUrl = 'https://github.com/TalosDx/J8-JME-JFX/issues'
        githubRepo = 'TalosDx/J8-JME-JFX'
        githubReleaseNotesFile = 'README.md'
        version {
            name = bintrayVersion
            desc = bintrayVersion
            released  = new Date()
        }
    }
}

dependencies {
    compile group: 'org.projectlombok', name: 'lombok', version: '1.18.6'
    compile group: 'org.jetbrains', name: 'annotations', version: '15.0'
    implementation 'com.spaceshift:rlib.common:7.0.0'
    compile "${jme3.g}:jme3-core:${jme3.v}"
    compile "${jme3.g}:jme3-lwjgl3:${jme3.v}"
}

javadoc {
    failOnError = false
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.7'
}